{
  parserClass="com.bamuel.spllang.parser.SPLParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="SPL"
  psiImplClassSuffix="Impl"
  psiPackage="com.bamuel.spllang.psi"
  psiImplPackage="com.bamuel.spllang.psi.impl"

  elementTypeHolderClass="com.bamuel.spllang.psi.SPLTypes"
  elementTypeClass="com.bamuel.spllang.psi.SPLElementType"
  tokenTypeClass="com.bamuel.spllang.psi.SPLTokenType"
}

splFile ::= item_*

item_ ::=  declaration_ | statement_ | preprocessor_ | function_ | element_
    declaration_ ::= api_ | field_ | local_ | menu_ | mode_ | object_ | parameters_ | procedure_ | returning_ | screen_
    statement_ ::= messagebox_ | message_
    preprocessor_ ::= preprocessors_ //Pre-processor commands summary //todo: split into individual preprocessor commands
    function_ ::= environmentfunction_ | stringfunction_
        environmentfunction_ ::= getenv_
        stringfunction_ ::= paramtext_ | concat_ | strconcat_ | str_


// Messagebox rule
messagebox_ ::= MESSAGEBOX string_ messsageboxtitle_? messageboxbuttons_? messageboxdefault_? messageboxicon_? messageboxtag_? messageboxhelpcontext_? messageboxoptional_? messageboxbell_? item_*

messsageboxtitle_ ::= TITLE ("is")? string_
messageboxbuttons_ ::= MESSAGEBOX_BUTTONS MESSAGEBOX_BUTTONS_VALUES
messageboxdefault_ ::= DEFAULT ("is")? MESSAGEBOX_BUTTONS_VALUES
messageboxicon_ ::= ICON ("is")? MESSAGEBOX_ICON_VALUES
messageboxtag_ ::= TAG ("is")? NUMBER
messageboxhelpcontext_ ::= HELP_CONTEXT string_
messageboxoptional_ ::= OPTIONAL
messageboxbell_ ::= BELL


// Function syntax
paramtext_ ::= PARAMTEXT LPAREN STRING (COMMA param_list_)? RPAREN
param_list_ ::= string_ (COMMA string_)*

concat_ ::= CONCAT LPAREN string_ (COMMA? string_)* RPAREN
strconcat_ ::= STRCONCAT LPAREN string_ (COMMA? string_)* RPAREN
str_ ::= STR_FUNC LPAREN number_ RPAREN

// Message rule
message_ ::= MESSAGE (string_)? (NOBELL)?

// Environment function
getenv_ ::= GETENV LPAREN string_ RPAREN

// Preprocessor rule
preprocessors_ ::= PREPROCESSOR (BAD_CHARACTER)?

// Screen rule
screen_ ::= SCREEN IDENTIFIER (SCREEN_RESPONSIVE)? (window_)? (windowpositision_)? (screentitle_)? (screenprimary_)? (screenselect_)? (screenquicklink_)? (colour_)? (screenreview_)? (screenallowed_)? (SCREEN_PROMPT_FOR_SEARCH)? (SCREEN_FIND_FOR_CURRENCY)? (screenprompt_)? (screendatagrid_)? (screenreviewoccurs)? (SCREEN_REVIEW_FROM_START)? (SCREEN_NO_REVIEW_ROW_SEPARATORS)? (SCREEN_NO_REVIEW_COLUMN_SEPARATORS)? (SCREEN_NO_REVIEW_SEPARATORS)? (SCREEN_START_ON_CURRENT_RECORD)? (SCREEN_LEAVE_PARENT_SCREEN)? (screenwhen_)? (local_)? (parameters_)? (returning_)? (STOP_EXIT_ALL_KEY)? (SCREEN_POSITION_ON_OK)? (SCREEN_STAY_IN_CORRECT)? (SCREEN_FORM_ENTRY)? (export_)? (screenhelpcontext_)? (before_)? (detail_)? (after_)? SCREEN_END

screentitle_ ::= TITLE ("is")? string_
screenprimary_ ::= SCREEN_PRIMARY IDENTIFIER (SCREEN_ON)? (SCREEN_SAME_DIFFERENT (IDENTIFIER fieldclauses_)*)?
screenselect_ ::= SCREEN_SELECT selectclauses_
screenquicklink_ ::= SCREEN_QUICK_LINK ("on")? IDENTIFIER
screenreview_ ::= SCREEN_REVIEW ("is")? "colour" ("on")? IDENTIFIER
screenallowed_ ::= SCREEN_ALLOWED ("is"|"are")? ("FIND"|"NEXT"|"PREV"|"NEXT-SCR"|"PREV-SCR"|"SEARCH"|"ENTRY"|"CORRECT"|"REMOVE"|"find"|"next"|"prev"|"next-scr"|"prev-scr"|"search"|"entry"|"correct"|"remove") IDENTIFIER* //todo: [mode-name ...]]
screenprompt_ ::= PROMPT rowcol_
screendatagrid_ ::= SCREEN_DATA_GRID item_* //todo: datagrid
screenreviewoccurs ::= SCREEN_REVIEW_OCCURS number_ ("by" number_)?
screenwhen_ ::= WHEN item_* //todo: conditon

// API rule
api_ ::= API IDENTIFIER item_* API_END //todo: procedural-statements

// Field rule
field_ ::= FIELD (IDENTIFIER fieldclauses_)*
fieldclauses_ ::= (fieldtype_)? (fieldpic_)? (fieldoccurs_)? (fieldlike_)? (fieldcase_)? (JUSTIFY)? (USE_NAME_IN_DB)? (fielddbcolumnname_)? (fielddrillback_)?
fieldtype_ ::= TYPE ("is")? datatypes_
datatypes_ ::= ("NUMERIC"|"DECIMAL"|"MONEY"|"FLOATING"|"ALPHA"|"STRING"|"VARCHAR"|"BLOB"|"XML-HANDLE"|"DATE"|"TIME"|"DATE-TIME"|"DATETIME"|"DATE_TIME"|"LOCAL-DATE-TIME"|"BOOLEAN"|"COLOUR"|"COLOR"|"OLE-INTERFACE"|"numeric"|"decimal"|"money"|"floating"|"alpha"|"string"|"varchar"|"blob"|"xml-handle"|"date"|"time"|"date-time"|"datetime"|"date_time"|"local-date-time"|"boolean"|"colour"|"color"|"ole-interface")
fieldpic_ ::= PIC ("is")? picture_
fieldoccurs_ ::= OCCURS NUMBER ("times")?
fieldlike_ ::= LIKE IDENTIFIER
fieldcase_ ::= ("UPPERCASE" | "LOWERCASE" | "uppercase" | "lowercase")
fielddbcolumnname_ ::= DB_COLUMN_NAME ("is")? string_
fielddrillback_ ::= DRILL_BACK ("is")? string_

// Local field rule
local_ ::= LOCAL (FIELD)? (IDENTIFIER fieldclauses_)*

// Menu rule
menu_ ::= MENU IDENTIFIER menucaluses_ MENU_END
menucaluses_ ::= (window_)? (windowpositision_)? (local_)? (parameters_)? (returning_)? (menubuttonwidth_)? (STOP_EXIT_ALL_KEY)? (TITLE ("is")? string_)? (TREEMENU)? (NOHIDE)? (colour_)? (export_)? (helpcontext_)? (before_)? (detail_)? (after_)?
menubuttonwidth_ ::= MENU_BUTTON_WIDTH ("is")? number_

// Mode rule
mode_ ::= MODE IDENTIFIER modeclauses_
modeclauses_ ::= (modeprompt_)? (help_)? (security_)? (modeentry_)? (process_)? (modecurrency_)? (LOCK)? (modeperform_)? (modewhen_)? (colour_)? (modeicon_)? (alwaysshowaddnohide_)?
modeprompt_ ::= PROMPT ("is")? string_
help_ ::= HELP ("is")? string_
security_ ::= SECURITY ("is")? number_
modeentry_ ::= MODEENTRY
process_ ::= PROCESS
modecurrency_ ::= MODECURRENCY
modeperform_ ::= PERFORM IDENTIFIER
modewhen_ ::= WHEN item_*
modeicon_ ::= ICON ("is")? modeiconlist_
modeiconlist_ ::= "FOLDER-ICON"
                | "ADMIN-FOLDER-ICON"
                | "REPORT-FOLDER-ICON"
                | "ADMIN-ICON"
                | "REPORT-ICON"
                | "SCREEN-ICON"
                | "TRANSACTION-ICON"
                | "PERIOD-END-ICON"
                | "QUESTION-ICON"
                | "BLANK-ICON"
                | "FAVOURITES-FOLDER-ICON"
                | "LOCKED-FOLDER-ICON"
                | "BLUE-FOLDER-ICON"
                | "RED-FOLDER-ICON"
                | "GREEN-FOLDER-ICON"
                | "ENQUIRY-FOLDER-ICON"
alwaysshowaddnohide_ ::= (ALWAYS_SHOW)? (ALWAYS_ADD)? (NOHIDE)?


// Object rule
object_ ::= OBJECT (IDENTIFIER objectclauses_)*
objectclauses_ ::= (objectlike_)? (objecttype_)? (objectseparator_)? (objectfile_)? (objectrecord_)? (objectkey_)? (NOJOINS)? (NOROWID)?
objectlike_ ::= LIKE IDENTIFIER
objecttype_ ::= TYPE ("is")? filetypes_
filetypes_ ::= ("EXTERNAL"|"TEXT"|"SEQUENTIAL"|"FIXED-LENGTH-TEXT"|"INDEXED"|"ISAM"|"PRN"|"CSV"|"DB-SEQUENTIAL"|"MEMORY"|"external"|"text"|"sequential"|"fixed-length-text"|"indexed"|"isam"|"prn"|"csv"|"db-sequential"|"memory")
objectseparator_ ::= SEPARATOR ("is")? string_
objectfile_ ::= FILE ("is")? string_
objectrecord_ ::= RECORD ("is")? (IDENTIFIER fieldclauses_)* RECORD_END
objectkey_ ::= KEY ("is")? (IDENTIFIER (DESC)? (UNIQUE)? (DB_INDEX_ONLY)? (COMPRESS)?)*

// Parameters rule
parameters_ ::= PARAMETERS ("are"|"is")? (IDENTIFIER fieldclauses_)*

// Returning rule
returning_ ::= RETURNING (IDENTIFIER fieldclauses_)*

// Procedure rule
procedure_ ::= PROCEDURE IDENTIFIER procedureclauses_ PROCEDURE_END//todo: procedural-statements
procedureclauses_ ::= (window_)? (relative_)? (NOHIDE)? (windowpositision_)? (colour_)? (local_)? (parameters_)? (returning_)? (STOP_EXIT_ALL_KEY)? (TITLE string_)? (export_)? (AUTO_TRANSACTION)? (helpcontext_)? item_*

// Other rules
export_ ::= EXPORT (CAN_OVERRIDE)?
colour_ ::= COLOUR ("is")? IDENTIFIER ("on" IDENTIFIER)?
helpcontext_ ::= SCREEN_HELP_CONTEXT ("is") string_ string_
window_ ::= WINDOW rowcol_ "to" rowcol_ (relative_)?
relative_ ::= RELATIVE_TO "to" rowcol_
windowpositision_ ::= WINDOW_POSITION ("is")? number_
before_ ::= BEFORE item_* //todo: procedural-statements
detail_ ::= DETAIL item_* //todo: procedural-statements
after_ ::= AFTER item_* //todo: procedural-statements

//row and col
//Syntax @row,col
rowcol_ ::= AT NUMBER COMMA NUMBER
selectclauses_ ::= "SELECT" item_* //todo: sql select

string_ ::= STRING | IDENTIFIER | BAD_CHARACTER | stringfunction_ | environmentfunction_
number_ ::= NUMBER | IDENTIFIER
//The following picture string characters are used to specify the format of a field being defined.
// They indicate the size of the field, number of integer/decimal places and whether the field allows negative amounts.
// todo: add more picture string characters, this covers the basics.
picture_ ::= pic_group_ | pic_words
pic_words ::= "ddmmmyybhh:mm"  // Date and time format
pic_group_ ::= (NUMBER | IDENTIFIER) (LPAREN number_ RPAREN)? (IDENTIFIER LPAREN number_ RPAREN)?

element_ ::= FUNCTION_DECLARATION
           | KEYWORD
           | STRING
           | NUMBER
           | IDENTIFIER
           | COMMENT
           | BLOCK_COMMENT
           | TITLE
           | DEFAULT
           | WINDOW
           | PROMPT
           | DETAIL
           | ICON
           | TAG
           | HELP_CONTEXT
           | OPTIONAL
           | BELL
           | NOBELL
           | COLOUR
           | TYPE
           | PIC
           | OCCURS
           | LIKE
           | JUSTIFY
           | USE_NAME_IN_DB
           | DB_COLUMN_NAME
           | DRILL_BACK
           | RELATIVE_TO
           | WINDOW_POSITION
           | PARAMETERS
           | RETURNING
           | STOP_EXIT_ALL_KEY
           | EXPORT
           | CAN_OVERRIDE
           | BEFORE
           | AFTER
           | SEPARATOR
           | FILE
           | RECORD
           | RECORD_END
           | KEY
           | DESC
           | UNIQUE
           | DB_INDEX_ONLY
           | COMPRESS
           | NOJOINS
           | NOROWID
           | HELP
           | PROCESS
           | MODEENTRY
           | MODECURRENCY
           | LOCK
           | PERFORM
           | ALWAYS_SHOW
           | ALWAYS_ADD
           | SCREEN_PRIMARY
           | LOCAL
           | MESSAGEBOX_BUTTONS_VALUES
           | MESSAGEBOX_ICON_VALUES
           | othersymbols_

othersymbols_ ::= SYMBOLS | SEMICOLON | DOT | EQUALS | LBRACE | RBRACE | LPAREN | RPAREN | COMMA | AT