{
  parserClass="com.bamuel.spllang.parser.PCSParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="PCS"
  psiImplClassSuffix="Impl"
  psiPackage="com.bamuel.spllang.psi"
  psiImplPackage="com.bamuel.spllang.psi.impl"

  elementTypeHolderClass="com.bamuel.spllang.psi.PCSTypes"
  elementTypeClass="com.bamuel.spllang.psi.PCSElementType"
  tokenTypeClass="com.bamuel.spllang.psi.PCSTokenType"
}

pcsFile ::= override_screen_definition_*

override_screen_definition_ ::= override_screen_definitionlists_ identifier_ LBRACE override_statements_ RBRACE
override_screen_definitionlists_ ::= SCREEN
                                   | MENU
                                   | PROCEDURE

override_statements_ ::= comments_? override_statementlists_* comments_?
override_statementlists_ ::= ACCEPT LBRACE  comments_? accept_clauses_*  comments_? RBRACE
                          |  DISPLAY LBRACE  comments_? display_clauses_*  comments_? RBRACE
                          |  OPTION LBRACE  comments_? option_clauses_*  comments_? RBRACE
                          |  MODE LBRACE  comments_? mode_clauses_*  comments_? RBRACE
                          |  CONFIRM LBRACE  comments_? confirm_clauses_*  comments_? RBRACE
                          |  TRIGGER LBRACE  comments_? trigger_clauses_*  comments_? RBRACE
                          |  WINDOW LBRACE  comments_? window_clauses_*  comments_? RBRACE
                          |  BOX LBRACE  comments_? box_clauses_*  comments_? RBRACE
                          |  MESSAGE LBRACE  comments_? message_clauses_*  comments_? RBRACE

accept_clauses_ ::= NAME EQUALS (STRING|identifier_)
                  | ORIG_COORDINATE EQUALS ANYCOORDINATES
                  | TAG EQUALS NUMBER
                  | HELPLINE EQUALS STRING
                  | TITLE EQUALS (STRING|identifier_)
                  | DEFAULT EQUALS (STRING|identifier_|NUMBER)
                  | ALLOW EQUALS LIST
                  | DISALLOW EQUALS LIST
                  | SUPPRESS EQUALS BOOLEAN
                  | READONLY EQUALS BOOLEAN
                  | SCALE EQUALS NUMBER
                  | COORDINATE EQUALS ANYCOORDINATES
                  | TITLE_COORDINATE EQUALS ANYCOORDINATES
                  | PICTURE EQUALS picture_
                  | clause_for_attribute_
                  | clause_for_color_
                  | TABORDER EQUALS (STRING|NUMBER)
                  | UNDOCUMENTED EQUALS (STRING|identifier_|NUMBER|COMMA|BOOLEAN)*

display_clauses_ ::= NAME EQUALS (STRING|identifier_)
                   | ORIG_COORDINATE EQUALS ANYCOORDINATES
                   | ORIG_TEXT EQUALS STRING
                   | TAG EQUALS NUMBER
                   | TEXT EQUALS STRING
                   | TITLE EQUALS (STRING|identifier_)
                   | ADD EQUALS BOOLEAN
                   | SUPPRESS EQUALS BOOLEAN
                   | SCALE EQUALS NUMBER
                   | COORDINATE EQUALS ANYCOORDINATES
                   | TITLE_COORDINATE EQUALS ANYCOORDINATES
                   | PICTURE EQUALS picture_
                   | clause_for_attribute_
                   | clause_for_color_
                   | UNDOCUMENTED EQUALS (STRING|identifier_|NUMBER|COMMA|BOOLEAN)*

option_clauses_ ::= ORIG_COORDINATE EQUALS ANYCOORDINATES
                  | ORIG_TEXT EQUALS STRING
                  | TEXT EQUALS STRING
                  | HELPLINE EQUALS STRING
                  | SUPPRESS EQUALS BOOLEAN
                  | COORDINATE EQUALS ANYCOORDINATES

mode_clauses_ ::= NAME EQUALS (STRING|identifier_)
               | TEXT EQUALS STRING
               | HELPLINE EQUALS STRING
               | ADD EQUALS BOOLEAN
               | SUPPRESS EQUALS BOOLEAN

confirm_clauses_ ::= NAME EQUALS (STRING|identifier_)
                  | TEXT EQUALS STRING
                  | DEFAULT EQUALS (STRING|identifier_|NUMBER)
                  | SUPPRESS EQUALS BOOLEAN
                  | COORDINATE EQUALS ANYCOORDINATES

trigger_clauses_ ::= TRIGGER_POINT EQUALS trigger_point_value_
                    | TRIGGER_TYPE EQUALS trigger_type_value_
                    | NAME EQUALS (STRING|identifier_)
                    | ORIG_TEXT EQUALS STRING
                    | RUN EQUALS LIST

trigger_point_value_ ::= SCREEN_LOAD | SCREEN_EXIT | BEFORE_ACCEPT | AFTER_ACCEPT | ON_HELP | BEFORE_CONFIRM | CONFIRMED | NOT_CONFIRMED | VALIDATE_MODE | OPTION | BEFORE_OPTION | DISPLAY | PRE_VALIDATE
trigger_type_value_ ::= WINDOWS | WINDOWS_WITH_STATUS | UNIX | COMMAND | UNIX_WITH_STATUS | COMMAND_WITH_STATUS | PRONTO | EXPORT_DATA_GRID | "clib"

window_clauses_ ::= TITLE EQUALS (STRING|identifier_)
                  | WINDOW_SIZE EQUALS ANYCOORDINATES

box_clauses_ ::= ORIG_COORDINATE EQUALS ANYCOORDINATES
               | TITLE EQUALS (STRING|identifier_)
               | ADD EQUALS BOOLEAN
               | SUPPRESS EQUALS BOOLEAN
               | COORDINATE EQUALS ANYCOORDINATES

message_clauses_ ::= TEXT EQUALS STRING

LIST ::= ((STRING|identifier_|NUMBER|ANYCOORDINATES) COMMA?)*
ANYCOORDINATES ::=(COORDINATES|FLOAT_COORDINATES)
identifier_ ::= IDENTIFIER (LSQUARE NUMBER RSQUARE)*

//The following picture string characters are used to specify the format of a field being defined.
// They indicate the size of the field, number of integer/decimal places and whether the field allows negative amounts.
// todo: add more picture string characters, this covers the basics.
picture_ ::= (DOT|NUMBER|STRING|identifier_|LPAREN|LSQUARE|RSQUARE|LPAREN|RPAREN|FLOAT|ANYCOORDINATES)*

clause_for_color_ ::= COLOR EQUALS color_value_
color_value_ ::= color_list_ SLASH? color_list_?
color_list_ ::= COLOR_AQUA
              | COLOR_BLACK
              | COLOR_BLUE
              | COLOR_FUCHSIA
              | COLOR_GREEN
              | COLOR_GREY
              | COLOR_LIME
              | COLOR_MAROON
              | COLOR_NAVY
              | COLOR_OLIVE
              | COLOR_PURPLE
              | COLOR_RED
              | COLOR_SILVER
              | COLOR_TEAL
              | COLOR_WHITE
              | COLOR_YELLOW
              | NUMBER

clause_for_attribute_ ::= ATTRIBUTES EQUALS attribute_value_*
attribute_value_ ::= attribute_lists_ COMMA?
attribute_lists_ ::= UNDERLINE
                   | UNDERLINED
                   | PROMPT
                   | DATA
                   | INVERSE
                   | BOLD
                   | ITALIC
                   | FLASH
                   | FIXED
                   | SUNKEN
                   | BACKGROUND
                   | CENTER
                   | RIGHT
                   | LEFT
                   | BORDER
                   | UNDOCUMENTED

// Define the comments
comments_ ::= comment_*
comment_ ::= COMMENT | BLOCK_COMMENT
